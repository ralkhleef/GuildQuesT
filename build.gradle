plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

javafx {
    version = "21.0.2"
    modules = [ 'javafx.controls' ]
}

application {
    mainClass = "guildquest.gui.GuiMain"
}

sourceSets {
    main {
        java {
            exclude 'guildquest/GuildQuestApp.java'
        }
    }
}

tasks.register('fatJar', Jar) {
    archiveBaseName.set('GuildQuest')
    archiveClassifier.set('all')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes('Main-Class': application.mainClass.get())
    }

    from(sourceSets.main.output)

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'GuildQuest'
    }
}